#!mpsl3

/** stock processor **/

/* symbols */
var symbols = ["REP.MC" "POP.MC" "MCD" "EURUSD=X"];

/* query arguments for Yahoo Finance */
var yf = {
    host:   "download.finance.yahoo.com"
    query:  "/d/quotes.csv?s=" ~  (symbols ~ ',') ~ "&f=sl1d1t1&e=.csv"
    proto:  'HTTP/1.0'
};

//var yf_query    = "/d/quotes.csv?s=" ~  (symbols ~ ',') ~ "&f=sl1d1t1c1ohgv&e=.csv";


/* http query and response */
sub http(args) {
    var s = NULL;

    if (s = sys.connect(args.host, 'www')) != NULL {
        /* query */
        s.write('GET ', args.query, ' ', (args.proto || 'HTTP/1.1'), "\r\n");

        /* headers */
        foreach args s.write(key, ': ', value, "\r\n");
        s.write("\r\n");

        /* response */
        var r = {}, l = s.read().sregex("/\r?\n$").split(' ');

        /* split first line */
        r['proto']  = l[0];
        r['status'] = l[1];
        r['stmsg']  = l[2];

        /* split rest of response header */
        while (l = s.read()) != NULL && !l.regex("/^\r?\n$/") {
            l = l.sregex("/\r?\n$/").split(': ');
            r[l[0]] = l[1];
        }

        s = {
            fd:     s
            resp:   r
        };
    }

    return s;
}

var www = http(yf);

if www != NULL {
    var o = [], l;

    foreach www.fd.read() {
        l = value.sregex("/\r?\n$/").sregex("/\"/g").split(',');

        o.push(
            {
                symbol:     l[0]
                value:      l[1] + 0
                time:       (l[2] ~ ' ' ~ l[3]).sscanf("%t{%m/%d/%Y %I:%M%p}")[0]
            }
        );
    }

    sys.p("%j" $ o, "\n");
}
